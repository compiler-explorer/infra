data "aws_caller_identity" "current" {}

resource "aws_cloudtrail" "audit" {
  name                          = "ce-audit"
  s3_bucket_name                = aws_s3_bucket.cloudtrail.id
  include_global_service_events = true
  event_selector {
    include_management_events = true
    read_write_type = "All"
  }
  is_multi_region_trail         = true
  enable_log_file_validation    = true
  depends_on = [aws_s3_bucket_policy.cloudtrail-bucket-policy]
  # until https://github.com/terraform-providers/terraform-provider-aws/pull/5448 is fixed
  lifecycle {
    ignore_changes = [event_selector]
  }
}

data "aws_iam_policy_document" "audit-s3-policy" {
  statement {
    principals {
      type        = "Service"
      identifiers = ["cloudtrail.amazonaws.com"]
    }
    sid       = "AWSCloudTrailAclCheck"
    actions   = ["s3:GetBucketAcl"]
    resources = [aws_s3_bucket.cloudtrail.arn]
  }
  statement {
    sid       = "AWSCloudTrailWrite"
    principals {
      type        = "Service"
      identifiers = ["cloudtrail.amazonaws.com"]
    }
    actions   = ["s3:PutObject"]
    resources = ["${aws_s3_bucket.cloudtrail.arn}/AWSLogs/${data.aws_caller_identity.current.account_id}/*"]
    condition {
      test     = "StringEquals"
      variable = "s3:x-amz-acl"
      values   = ["bucket-owner-full-control"]
    }
  }
}

resource "aws_s3_bucket" "cloudtrail" {
  bucket        = "cloudtrail.godbolt.org"
  force_destroy = true

  // TODO one day
  //  versioning {
  //    mfa_delete = true
  //  }

  tags                          = {
    S3-Bucket-Name = "cloudtrail.godbolt.org"
  }

  lifecycle_rule {
    enabled = true
    expiration {
      days = 200
    }
    noncurrent_version_expiration {
      days = 1
    }
  }
}

resource "aws_s3_bucket_policy" "cloudtrail-bucket-policy" {
  bucket = aws_s3_bucket.cloudtrail.id
  policy = data.aws_iam_policy_document.audit-s3-policy.json
}

resource "aws_s3_bucket_public_access_block" "cloudtrail" {
  bucket = aws_s3_bucket.cloudtrail.id
  block_public_acls   = true
  block_public_policy = true
  ignore_public_acls = true
  restrict_public_buckets = true
}
